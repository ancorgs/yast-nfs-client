/**
 * File:
 *   Nfs.ycp
 *
 * Module:
 *   Configuration of nfs
 *
 * Summary:
 *   NFS client configuration data, I/O functions.
 *
 * Authors:
 *   Jan Holesovsky <kendy@suse.cz>
 *   Dan Vesely <dan@suse.cz>
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 */

{
    module "Nfs";

    textdomain "nfs";

    import "Report";
    import "Runlevel";

    /**
     * eg.: [ $["spec": "moon:/cheese", file: "/mooncheese", "mntopts": "defaults"], ...]
     */
    global list(map) nfs_entries = [];

    /**
     * Read only, intended for checking mount-point uniqueness.
     */
    global list(map) non_nfs_entries = [];

    /**
     * Get all NFS configuration from a map.
     * When called by nfs_auto (preparing autoinstallation data)
     * the map may be empty.
     * @param settings	a map with a single key: nfs_entries
     * @return	success
     */
    global define boolean Import (map settings) ``{
	if (size (settings) == 0)
	{
	    //Provide defaults for autoinstallation editing:
	    //Leave empty.
	    return true;
	}

	boolean missing = false;
	foreach (`k, ["nfs_entries"], ``{
	    if (! haskey (settings, k))
	    {
		y2error ("Missing at Import: '%1'.", k);
		missing = true;
	    }
	});
	if (missing)
	{
	    return false;
	}

	nfs_entries = maplist(map entry, lookup (settings, "nfs_entries", []), ``{
		return($[
			"spec":entry["server_path"]:"",
			"file":entry["mount_point"]:"",
			"mntops":entry["nfs_options"]:""
			]);
	});
	return true;
    }

    /**
     * Dump the NFS settings to a map, for autoinstallation use.
     * @return a map containing a single map.
     */
    global define map Export () ``{
	list entries = maplist(map entry, nfs_entries, ``{
		return($[
			"server_path":entry["spec"]:"",
			"mount_point":entry["file"]:"",
			"nfs_options":entry["mntops"]:""
			]);
	});
	return $[
	    "nfs_entries": entries,
	    ];
    }

    /**
     * Reads NFS settings from the SCR (.etc.fstab)
     * @return true on success
     */
    global define boolean Read () ``{
	list(map) fstab = SCR::Read (.etc.fstab);
	// For simplicity, this leaves also the unused fileds in the maps.
	nfs_entries = filter (`entry, fstab, ``{
	    return (lookup (entry, "vfstype", "") == "nfs");
	});
	non_nfs_entries = filter (`entry, fstab, ``{
	    return (lookup (entry, "vfstype", "") != "nfs");
	});

	return true;
    }

    /**
     * Writes the NFS client configuration and starts/stops the service.
     * (No parameters because it is too short to abort)
     * @return true on success
     */
    global define boolean Write () ``{
	if (WriteOnly ())
	{
	    Runlevel::RunInitScript ("nfs", "stop");
	    if (size (nfs_entries) > 0)
	    {
		if (Runlevel::ServiceStatus ("portmap") != 0)
		{
		    // portmap must not be started if it is running already (see bug # 9999)
		    Runlevel::RunInitScript ("portmap", "start");
		}
		Runlevel::RunInitScript ("nfs", "start");

		if (Runlevel::ServiceStatus ("nfs") != 0)
		{
		    // error popup message
		    Report::Error (_("Unable to mount the NFS entries from /etc/fstab."));
		    return false;
		}
	    }
	    return true;
	}
	else
	{
	    return false;
	}
    }
    /**
     * Writes the NFS client configuration without
     * starting/stopping the service.
     * Autoinstallation uses this and then calls SuSEconfig only once
     * and starts the services together.
     * (No parameters because it is too short to abort)
     * @return true on success
     */
    global define boolean WriteOnly () ``{
	// Merge with non-nfs entries from fstab:
	list(map) fstab = SCR::Read (.etc.fstab);
	fstab = filter (`entry, fstab, ``{
	    return (lookup (entry, "vfstype", "") != "nfs");
	});
	foreach (`entry, nfs_entries, ``{
	    fstab = add (fstab, union (entry, $["vfstype": "nfs", "freq": 0, "passno": 0]));

	    // create mount points
	    string file = lookup(entry, "file", "");
	    if (! SCR::Execute (.target.mkdir, file))
	    {
		// error popup message
		Report::Error (sformat (_("Unable to create directory '%1'."), file));
		return false;
	    }
	});


	SCR::Execute (.target.bash, "/bin/cp $ORIG $BACKUP", $["ORIG" : "/etc/fstab", "BACKUP" : "/etc/fstab.YaST2.save"]);

	if (SCR::Write(.etc.fstab, fstab) == nil)
	{
	    // error popup message
	    Report::Error (_("Unable to write to /etc/fstab.
There will be no changes in the
configuration of the NFS client.
"));
	    return false;
	}

	if (size (nfs_entries) == 0)
	{
	    //Runlevel::ServiceAdjust ("nfs", "disable"); // what if autofs needs it?
	}
	else
	{
	    Runlevel::ServiceAdjust ("portmap", "enable");
	    Runlevel::ServiceAdjust ("nfs", "enable");
	}

	return true;
    }

}
