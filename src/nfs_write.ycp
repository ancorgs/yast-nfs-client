/**
 * File:
 *   nfs_write.ycp
 *
 * Module:
 *   Configuration of nfs
 *
 * Summary:
 *   Writing only client
 *
 * Authors:
 *   Jan Holesovsky <kendy@suse.cz>
 *   Dan Vesely <dan@suse.cz>
 *
 * $Id$
 *
 * This is a write-only client. It takes its arguments and just
 * write the settings.
 */

{

    textdomain "nfs";

    include "ui/common_popups.ycp";


    /**
     * Saves NFS client part
     * @param import_nfs start NFS client
     * @param fstab list of fstab entries
     * @return boolean success
     */
    global define nfs_client_save (boolean import_nfs, list fstab) ``{

	SCR::Execute(.target.bash, "/etc/init.d/nfs stop", $[]);

	if (!import_nfs)
	{
	    // filter out any nfs entries
	    fstab = filter(`entry, fstab, ``{
		return (lookup(entry, "vfstype") != "nfs");
	    });
	}
	else
	{
	    // create mount points
	    foreach(`entry, fstab, ``{
		if (lookup(entry, "vfstype") == "nfs") {
		    import_nfs = true;
		    if (! SCR::Execute (.target.mkdir, lookup(entry, "file")))
		    {
			// error popup message
			UI::MessagePopup(sformat(_("Unable to create directory '%1'."), lookup(entry, "file")));
			return false;
		    }
		}

	    });
	}

	SCR::Execute (.target.bash, "/bin/cp $ORIG $BACKUP", $["ORIG" : "/etc/fstab", "BACKUP" : "/etc/fstab.YaST2.save"]);

	if (fstab != nil && SCR::Write(.etc.fstab, fstab) == nil)
	{
	    // error popup message
	    UI::MessagePopup(_("Unable to write to /etc/fstab.
There will be no changes in the
configuration of the NFS client.
"));
	    return false;
	}

	if (import_nfs)
	{
	    CallFunction (`runlevel_adjust ("portmap", "enable"));
	    CallFunction (`runlevel_adjust ("nfs", "enable"));

	    if (SCR::Execute(.target.bash, "/etc/init.d/portmap status") != 0)
	    {
		// portmap must not be started if it is running already (see bug # 9999)
		SCR::Execute(.target.bash, "/etc/init.d/portmap start");
	    }
	    SCR::Execute(.target.bash, "/etc/init.d/nfs start");

	    if (SCR::Execute(.target.bash, "/etc/init.d/nfs status") != 0)
	    {
		// error popup message
		UI::MessagePopup(_("Unable to mount the NFS entries from /etc/fstab."));
		return false;
	    }
	}

	return true;
    }


}
